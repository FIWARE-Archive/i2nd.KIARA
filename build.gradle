plugins {
  id "com.bmuschko.nexus" version "2.1.1"
}
// internal plugins
apply plugin: 'java'
apply plugin: 'maven'   // only needed to generate POM or to upload artifacts to maven repos.
apply plugin: 'eclipse' // Eclipse integration
apply plugin: 'idea'    // InteliJ IDEA integration
apply plugin: 'project-report'

//general & artifact properties
group = 'org.fiware.kiara'
version = '0.2.0'
description = "KIARA Advanced Middleware main project"

sourceCompatibility = 1.7
targetCompatibility = 1.7

// In this section you declare where to find the dependencies of your project
// see also: http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html#N10621
repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version:'17.0'
    compile group: 'io.netty', name: 'netty-all', version:'4.0.21.Final'
    compile group: 'org.slf4j', name: 'slf4j-jdk14', version:'1.7.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.3.3'
    compile group: 'org.fiware.kiara', name: 'kiaraparser', version: '0.2.0'
    testCompile group: 'junit', name: 'junit', version:'4.10'
}

test {
    testLogging {
      //events "started" // uncomment to also log the start of a test.
      events "passed", "skipped", "failed"
      // show standard out and standard error of the test JVM(s) on the console
      //showStandardStreams = false
    }
    // print summary
    afterSuite { desc, result ->
      if (!desc.parent) { // will match the outermost suite
        println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
      }
    }
}

modifyPom {
    project {
        name 'KIARA Advanced Middleware'
        artifactId 'KIARA'
        description project.description
        url 'https://github.com/FIWARE-Middleware'
        inceptionYear '2014'
        
        scm {
          connection 'scm:git:https://github.com/FIWARE-Middleware/KIARA.git'
          developerConnection 'scm:git:https://github.com/FIWARE-Middleware/KIARA.git'
          url 'https://github.com/FIWARE-Middleware/KIARA.git'
        }

        licenses {
          license {
            name 'GNU Lesser General Public License, Version 3.0'
            url 'https://www.gnu.org/licenses/lgpl.html'
          }
        }

        developers {
          developer {
            id 'richiprosima'
            name 'Riccardo Gonzalez'
            email 'RicardoGonzalez@eprosima.com'
          }
          developer {
            id 'RafaelLaraAlvarez'
            name 'Rafael Lara Alvarez'
            email 'RafaelLara@eprosima.com'
          }
          developer {
            id 'dmrub'
            name 'Dmitri Rubinstein'
            email 'Dmitri.Rubinstein@dfki.de'
          }
          developer {
            id 'mach'
            name 'Christof Marti'
            email 'mach@zhaw.ch'
          }
        }
    }
}

extraArchive {
    sources = true
    javadoc = true
    tests = false
}

nexus {
    sign = true
    // To upload to OSSRH comment/remove the following urls
    //repositoryUrl = "file:$buildDir/repos/"
    //snapshotRepositoryUrl = "file:$buildDir/repos/"
}

